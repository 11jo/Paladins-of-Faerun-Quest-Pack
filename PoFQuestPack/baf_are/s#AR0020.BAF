IF 
	True()
	Global("S#setAR0020","AR0020",0)  //   LES PORTES DE LA VILLE
THEN
	RESPONSE #100
		CreateCreature("s#mage1",[680.555],2)  // Mage de Padhiver
		SetGlobal("S#setAR0020","AR0020",1)
		Continue()
END

IF
	Global("Reveal0020","AR0020",0)
	Dead("XGLOFLT")  // Leader of Vagrant Daggers  // Лидер бродячих кинжалов
THEN
	RESPONSE #100
		SetGlobal("Reveal0020","AR0020",1)
		StartCutSceneMode()
		ClearAllActions()
		CreateCreature("XGSHTA",[834.473],8)  // Voleur de l'ombre  // Теневой Вор  // Shadow Thief
		Wait(1)
		DisplayStringHead("XGSHTA",@0)  // Bon travail mon seigneur, Aran vous a offert un présent.
		Wait(5)
		GiveItemCreate("XGCBOW3",Player1,0,0,0)  // Crossbow of Discharge  // Bleskova kuse
		GiveItemCreate("XGHAL01",Player1,0,0,0)  // Stone Halberd  // Kamenna Halapartna
		Wait(2)
		ActionOverride("XGSHTA",EscapeArea())
		EndCutSceneMode()
		Continue()
END

IF
	Global("XGDEADW","AR0020",0)
	Dead("XGprac")  // Haut prêtre de Cyric  // Верховный священник Цирика  // High priest of Cyric
THEN
	RESPONSE #100
		SetGlobal("XGDEADW","AR0020",1)
		StartCutSceneMode()
		ClearAllActions()
		CreateCreature("XGprtcq",[834.473],8)  // Grand prêtre de Torm  // Верховный священник Торма  // High priest of Torm
		Wait(1)
		DisplayStringHead("XGprtcq",@1)  // <CHARNAME> ! Dieu merci, je vous ai retrouvé. Nous vous sommes reconnaissants pour la construction du monastère, mais nous avons encore besoin d'aide. Rejoignez les Montagnes du Couchant dès que possible.
		Wait(5)
		HideAreaOnMap("XG0081")  // (PoF)  Montagnes du Couchant
		HideAreaOnMap("XG0082")  // (PoF)  Montagnes du Couchant
		HideAreaOnMap("XG0083")  // (PoF)  Montagnes du Couchant
		RevealAreaOnMap("XG0681")  // (PoF)  Monastère des admirateur de Torm
		RevealAreaOnMap("XG1081")  // (PoF)  Monastère des admirateur de Torm
		Wait(2)
		ActionOverride("XGprtcq",EscapeArea())
		EndCutSceneMode()
		Continue()
END

IF
	Global("XGDEADE","AR0020",0)
	Dead("XGptec")  // ~Воинственный священник~ // pretre combattant
THEN
	RESPONSE #100
		SetGlobal("XGDEADE","AR0020",1)
		StartCutSceneMode()
		ClearAllActions()
		CreateCreature("XGclc3",[834.473],8)  // Haut prêtre de Cyric  // Верховный священник Цирика // High priest of Cyric
		Wait(1)
		ActionOverride("XGclc3",MoveToObjectNoInterrupt(Protagonist))
		Wait(2)
		DisplayStringHead("XGclc3",@2)  // <CHARNAME> ! Dieu merci, je vous ai retrouvé. Nous vous sommes reconnaissants pour la construction du monastère, mais nous avons encore besoin d'aide. Rejoignez les Montagnes du Couchant dès que possible.
		Wait(5)
		HideAreaOnMap("XG8100")  // (PoF)  Montagnes du Couchant (bis)
		HideAreaOnMap("XG8200")  // (PoF)  Montagnes du Couchant
		HideAreaOnMap("XG8300")  // (PoF)  Montagnes du Couchant
		RevealAreaOnMap("XG8500")  // (PoF)  Monastère des admirateurs de Cyric
		RevealAreaOnMap("XG8101")  // (PoF)  Monastère des admirateurs de Cyric
		Wait(2)
		ActionOverride("XGclc3",EscapeArea())
		EndCutSceneMode()
		Continue()
END



