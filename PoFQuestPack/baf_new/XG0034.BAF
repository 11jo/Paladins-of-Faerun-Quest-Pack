IF
	Global("RevealAreaXG0034","XG0034",0)
	Dead("XGgghc")  // Хранитель врат
THEN
	RESPONSE #100
		SetGlobal("RevealAreaXG0034","XG0034",1)
END

IF
	Global("RevealAreaXG0034","XG0034",1)
	Dead("XGHC")  // Архимаг
THEN
	RESPONSE #100
		SetGlobal("RevealAreaXG0034","XG0034",2)
		ClearAllActions()
		StartCutSceneMode()
		HideGUI()
		Wait(1)
		BattleSong()
		ActionOverride(Player1,MoveToPoint([759.477]))
		ActionOverride(Player2,MoveToPoint([821.467]))
		ActionOverride(Player3,MoveToPoint([870.412]))
		ActionOverride(Player4,MoveToPoint([658.451]))
		ActionOverride(Player5,MoveToPoint([694.372]))
		ActionOverride(Player6,MoveToPoint([799.351]))
		MoveViewPoint([741.500],SLOW)
		Wait(2)
		CreateCreature("XGARTEN",[859.613],6)  // Артемис Энтрери
		CreateCreature("XGthef1",[900.643],6)  // Воришка
		CreateCreature("XGthef2",[830.665],6)  // Воришка
		CreateCreature("XGthef3",[887.702],6)  // Воришка
		CreateCreature("XGthef4",[867.733],6)  // Воришка
		Wait(6)
		DisplayStringHead("XGARTEN",@0)  // Are you again? Isn't that what happened at Askatle for you?
		Wait(6)
		DisplayStringHead(Player1,@1)  // Who are you? And what are you doing here?
		Wait(6)
		DisplayStringHead("XGARTEN",@2)  // Well, let's get to know you, I'm Artemis Entreri. I heard that you were arrested at Askatla, don't take it to heart, but now go away or die.
		Wait(7)
		ActionOverride("XGARTEN",Attack(Player1))
		ActionOverride(Player1,Attack("XGARTEN"))  // Артемис Энтрери
		Wait(15)
		ClearAllActions()
		DisplayStringHead("XGARTEN",@3)  // I don't have time for this, do it!
		Wait(6)
		ActionOverride("XGARTEN",EscapeArea())
		ActionOverride("XGthef1",Enemy())
		ActionOverride("XGthef2",Enemy())
		ActionOverride("XGthef3",Enemy())
		ActionOverride("XGthef4",Enemy())
		Wait(2)
		UnhideGUI()
		EndCutSceneMode()
END

IF
	Global("RevealAreaXG0034","XG0034",2)
	Dead("XGthef1")  // Воришка
	Dead("XGthef2")  // Воришка
	Dead("XGthef3")  // Воришка
	Dead("XGthef4")  // Воришка
THEN
	RESPONSE #100
		SetGlobal("RevealAreaXG0034","XG0034",3)
		ClearAllActions()
		StartCutSceneMode()
		Wait(2)
		DisplayStringHead(Player2,@4)  // <CHARNAME>! We must follow him, he ran there.
		Wait(5)
		RevealAreaOnMap("XG0020")
		Wait(2)
		ActionOverride(Player2,LeaveAreaLUA("XG0020","",[3782.3065],8))
		ActionOverride(Player1,LeaveAreaLUA("XG0020","",[3818.3026],8))
		ActionOverride(Player3,LeaveAreaLUA("XG0020","",[3877.3112],8))
		ActionOverride(Player4,LeaveAreaLUA("XG0020","",[3802.3159],8))
		ActionOverride(Player5,LeaveAreaLUA("XG0020","",[3660.3114],8))
		ActionOverride(Player6,LeaveAreaLUA("XG0020","",[3707.3156],8))
		EndCutSceneMode()
END

